source read-patterns
  @:[[]];
  $IN::readline -> #
  $@!

  when <=''> do ..|@: []; $IN::readline -> #
  otherwise ..|@(last): [$...]; $IN::readline -> #
end read-patterns

def patterns: $read-patterns;

templates find-horizontal-mirror&{smudge:}
  templates is-mirror
    @: 0;
    def reflection: $::length ~/ 2;
    def top: $(1..$reflection);
    $(last..~$reflection:-1) -> \[y;x](<~=$top($y;$x)> @is-mirror: $@is-mirror + 1;\) -> !VOID
    $@ -> #
    when <=$smudge::raw> do $reflection !
  end is-mirror

  def in: $;
  1..$in::length -> #

  when <> do
    def top: $;
    $in($top..last) -> \(<?($::length mod 2 <=0>)> $!\) -> is-mirror -> $ + $top - 1!
    def bottom: $;
    $in(first..$bottom) -> \(<?($::length mod 2 <=0>)> $!\) -> is-mirror !
end find-horizontal-mirror

templates transpose
  def in: $;
  $in(1) -> \[i]($in(first..last; $i)! \) !
end transpose

templates find-mirror-values&{smudge:}
  def in: $;
  [$ -> find-horizontal-mirror&{smudge: $smudge} -> $ * 100] -> #
  when <[](0)> do
    $in -> transpose -> find-horizontal-mirror&{smudge: $smudge} !
  otherwise $... !
end find-mirror-values

test 'example'
  def patterns: [
    [
      ['#.##..##.'...],
      ['..#.##.#.'...],
      ['##......#'...],
      ['##......#'...],
      ['..#.##.#.'...],
      ['..##..##.'...],
      ['#.#.##.#.'...]
    ],[
      ['#...##..#'...],
      ['#....#..#'...],
      ['..##..###'...],
      ['#####.##.'...],
      ['#####.##.'...],
      ['..##..###'...],
      ['#....#..#'...]
    ]];

  assert $patterns... -> find-mirror-values&{smudge: 0} -> ..=Sum&{of: :()} <=405> ''
  assert $patterns... -> find-mirror-values&{smudge: 1} -> ..=Sum&{of: :()} <=400> ''
end 'example'

// part 1
$patterns... -> find-mirror-values&{smudge: 0} -> ..=Sum&{of: :()} -> !OUT::write
'
' -> !OUT::write

// part 2
$patterns... -> find-mirror-values&{smudge: 1} -> ..=Sum&{of: :()} -> !OUT::write
